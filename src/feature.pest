nl = _{ "\n" | "\r\n" }
space = _{ " " | "\t" }
comment = _{ "#" ~ (!nl ~ any)* ~ nl }
whitespace = _{ " " }

docstring_quote = _{ "\"\"\"" }

docstring = {
    docstring_quote
    ~ nl ~ space*
    ~ docstring_value
    ~ space*
    ~ docstring_quote
}

docstring_value = {
    (
        !(
            (space)*
            ~ docstring_quote
        )
        ~ any
    )*
}

table_field = {
    (
        !("|")
        ~ any
    )+
}

table_header = {
    nl?
    ~ space*
    ~ "|"
    ~ (
        !nl ~
        table_field
        ~ "|"
    )+
}

table_row = {
    nl?
    ~ space*
    ~ "|"
    ~ (
        !nl ~
        table_field
        ~ "|"
    )+
}

datatable = {
    table_header
    ~ (
        table_row
    )+
}

step_kw = {
    ("Given" | "When" | "Then" | "And" | "But")
}

step_body = {
    (
        !(nl)
        ~ any
    )+
}

step = {
    step_kw
    ~ space*
    ~ step_body
    ~ nl
    ~ space*
    ~ (
        (datatable | docstring)
        ~ space*
        ~ nl
    )?
}

scenario_kw = _{
    ("Scenario" | "Example")
    ~ ":" ~ space*
}

scenario_outline_kw = _{ "Scenario Outline:" ~ space* }
examples_kw = _{ "Examples:" }

scenario_name = {
    (
        !(nl)
        ~ any
    )+
}

scenario_steps = {
    (step)+
}

scenario = {
    (space | nl)*
    ~ scenario_kw ~ scenario_name ~ nl
    ~ scenario_steps ~ nl?
}

scenario_outline = {
    (space | nl)*
    ~ scenario_outline_kw ~ scenario_name ~ nl
    ~ scenario_steps ~ nl+
    ~ examples_kw ~ datatable ~ nl
}

background_kw = _{
    "Background:" ~ space*
}

background = {
    (space | nl)*
    ~ background_kw ~ nl
    ~ (step)+ ~ nl
}

feature_kw = {
    "Feature:" ~ space*
}

feature_body = {
    (
        !(nl)
        ~ any
    )+
}

feature = {
    space*
    ~ feature_kw ~ feature_body ~ nl+
    ~ background?
    ~ (scenario_outline | scenario)*
}

main = {
    soi
    ~ (space | nl)*
    ~ feature
    ~ eoi
}